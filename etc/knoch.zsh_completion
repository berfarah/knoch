#compdef knoch
#autoload

# For reference:
# https://github.com/zsh-users/zsh-completions/blob/master/zsh-completions-howto.org

__knoch_projects() {
  projects=($(knoch ls --name-only))
  if [ $#projects -eq 0 ]; then
    _message "No projects found"
    return
  fi
  _values "projects" $projects
}

_knoch() {
  local -a commands

  commands=(
    bundle:'Download or update tracked repositories'
    add:'Clone and track a repository locally'
    remove:'Remove and stop tracking repository'
    list:'List tracked repositories'
    show:'Show full path of selected project'
    open:'Open the selected project in $EDITOR'
  )

  _arguments \
    "1: :{_describe 'command' commands}" \
    '*:: :->args'

  [ "$state" != "args" ] && return 0

  case $words[1] in
    bundle)
      ;;
    ls|list)
      _arguments '--name-only[list only repo names]'
      ;;
    add)
      _arguments '1:dir:_path_files -/'
      ;;
    rm|remove|show|open)
      _arguments '1:project:__knoch_projects'
      ;;
  esac
}

_knoch "$@"
